/*
 * This is our main "landing page" for our Hyper IDE help files.
 */
h2
  innerValue:Hyper IDE

img
  style:"margin-left:1rem; float:right;max-width: 25%;"
  src:"https://phosphorusfive.files.wordpress.com/2017/12/ufo.png"

p
  innerValue:@"Hyper IDE is a web based integrated development environment, in addition to also doubling as a file manager. If you're
interested in creating your own Hyperlambda applications, or editing your files on your server - Hyper IDE should be your preferred starting point."

p
  innerValue:@"Although Hyper IDE is very strong when creating Hyperlambda and Phosphorus Five apps and modules,
it supports more than 100 programming languages out of the box, in addition to integration plugins, allowing
you to create compiler services, integrate towards GIT, and many additional features. It arguably is a complete
alternative to Visual Studio or Eclipse. Hyper IDE is extremely easily extended, allowing you to create your
own plugins, and extend Hyper IDE in any ways you see fit."


/*
 * All files at "root" of sub section.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@IDE/help-files/Hyper IDE/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
