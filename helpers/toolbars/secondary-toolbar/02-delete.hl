/*
 * Deletes active file or folder toolbar button.
 */
button
  innerValue:@"<span class=""icon-bin""></span>"
  title:Deletes file or folder
  disabled
  events

    /*
     * Sink invoked when active item in file explorer has been changed.
     */
    hyper-ide.file-explorer.item-changed

      /*
       * Checking if there is a selected item.
       */
      if:x:/../*/_arg?value

        /*
         * Enabling button.
         */
        delete-widget-property:x:/../*/_event?value
          disabled

      else

        /*
         * Disabling button since there is no active item in file explorer.
         */
        set-widget-property:x:/../*/_event?value
          disabled

    /*
     * Deletes a file.
     */
    hyper-ide._delete-file

      /*
       * Retrieving file name.
       */
      hyper-ide.file-explorer.get-active-item

      /*
       * Then passing filename into modal widget.
       */
      eval-x:x:/+/**/.filename
      create-widgets
        micro.widgets.modal:hyper-ide-confirm-delete-file
          widgets
            h3
              innerValue:Confirm deletion
            literal
              element:p
              style:"float:left;font-size:5rem;margin-right:.5rem;"
              class:icon-bin
            p
              innerValue:@"Are you sure you want to delete the <code>{0}</code> file? I need you to confirm this action before I allow you to do this."
                :x:/@hyper-ide.file-explorer.get-active-item?value
            p
              innerValue:@"<strong>Warning, this action is permanent!</strong>"
            div
              class:right
              widgets
                div
                  class:strip
                  widgets
                    button
                      innerValue:Yes
                      oninit

                        /*
                         * Setting initial focus to "Yes" button.
                         */
                        micro.page.set-focus:x:/../*/_event?value

                      onclick

                        /*
                         * Forward evaluated above.
                         */
                        .filename:x:/../*/hyper-ide.file-explorer.get-active-item?value

                        /*
                         * Deleting actual file.
                         */
                        delete-file:x:/@.filename?value

                        /*
                         * Deleting selected item from tree.
                         *
                         * Notice, selected tree view item will always be the file
                         * user is trying to delete.
                         */
                        micro.widgets.tree.get-selected-items:hyper-ide-folder-tree-browser
                        add:x:/../*/micro.widgets.tree.delete-items
                          src:x:/@micro.widgets.tree.get-selected-items/*
                        micro.widgets.tree.delete-items:hyper-ide-folder-tree-browser

                        /*
                         * Making sure we close editor for file, if there exists
                         * at least one editor that is open.
                         */
                        hyper-ide.editors.get-open-editors
                        if:x:/@hyper-ide.editors.get-open-editors/*?count
                          >:int:0
                          eval-x:x:/+/*
                          hyper-ide.editors.close
                            filter:x:/@.filename?value
                            exact:bool:true
                            force:bool:true

                        /*
                         * Notice, in case file that was deleted didn't have a CodeMirror
                         * editor for its file extension, there might still exist
                         * a toolbar for it. Hence, checking if it does, and deleting
                         * the toolbar, if it exists.
                         */
                        p5.web.widgets.find-first:hyper-ide-toolbar-wrapper
                          .toolbar:x:/@.filename?value
                        if:x:/@p5.web.widgets.find-first/*/*?value
                          delete-widget:x:/@p5.web.widgets.find-first/*/*?value

                        /*
                         * Deleting modal window.
                         */
                        delete-widget:hyper-ide-confirm-delete-file

                    button
                      innerValue:No
                      onclick

                        /*
                         * Simply deleting modal widget.
                         */
                        delete-widget:hyper-ide-confirm-delete-file

    /*
     * Deletes a folder.
     */
    hyper-ide._delete-folder

      /*
       * Retrieving folder name.
       */
      hyper-ide.file-explorer.get-active-item

      /*
       * Deleting folder, asking user to confirm the action.
       */
      create-widgets
        micro.widgets.modal:hyper-ide-confirm-delete-folder
          widgets
            h3
              innerValue:Confirm deletion
            literal
              element:p
              style:"float:left;font-size:5rem;margin-right:.5rem;"
              class:icon-bin
            p
              innerValue:@"Are you sure you want to delete the <code>{0}</code> folder? This will delete all files and sub-folders, 
and close all editors that are editing files inside of your folder. I need you to confirm this action before I allow you to do this."
                :x:/@hyper-ide.file-explorer.get-active-item?value
            p
              innerValue:@"<strong>Warning, this action is permanent!</strong>"
            div
              class:right
              widgets
                div
                  class:strip
                  widgets
                    button
                      innerValue:Yes
                      oninit

                        /*
                         * Setting initial focus to "Yes" button.
                         */
                        micro.page.set-focus:x:/../*/_event?value

                      onclick

                        /*
                         * Figuring out which folder user is trying to delete.
                         */
                        get-widget-property:hyper-ide-active-folder-toolbar
                          .folder

                        /*
                         * Deleting actual folder.
                         */
                        delete-folder:x:/@get-widget-property/*/*?value

                        /*
                         * Deleting selected item from tree.
                         *
                         * Notice, selected tree view item will always be the file or folder 
                         * user is trying to delete, since selecting a tree node, 
                         * changes the active file object edited.
                         */
                        micro.widgets.tree.get-selected-items:hyper-ide-folder-tree-browser
                        add:x:/../*/micro.widgets.tree.delete-items
                          src:x:/@micro.widgets.tree.get-selected-items/*
                        micro.widgets.tree.delete-items:hyper-ide-folder-tree-browser

                        /*
                         * Making sure we close all editors that are editing files beneath
                         * currently deleted folder.
                         */
                        eval-x:x:/+/*
                        hyper-ide.editors.close
                          filter:x:/@get-widget-property/*/*?value
                          force:bool:true

                        /*
                         * Checking if folder toolbar exists, and deleting it if it does.
                         */
                        if
                          fetch:x:/0/0?value
                            widget-exists:hyper-ide-active-folder-toolbar
                          delete-widget:hyper-ide-active-folder-toolbar

                        /*
                         * Raising changed event.
                         */
                        hyper-ide.file-explorer.item-changed

                        /*
                         * Deleting modal window.
                         */
                        delete-widget:hyper-ide-confirm-delete-folder

                    button
                      innerValue:No
                      onclick

                        /*
                         * Simply deleting modal widget.
                         */
                        delete-widget:hyper-ide-confirm-delete-folder

  onclick

    /*
     * Checking if user is trying to delete a file, or a folder.
     */
    hyper-ide.file-explorer.get-active-item
    if
      ends-with:x:/@hyper-ide.file-explorer.get-active-item?value
        src:/

      /*
       * Deletes a folder.
       */
      hyper-ide._delete-folder

    else

      /*
       * Deletes a file.
       */
      hyper-ide._delete-file
