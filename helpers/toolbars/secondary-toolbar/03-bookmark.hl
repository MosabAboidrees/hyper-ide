/*
 * Bookmark toolbar button. Creates a textbox containing the URL to open up
 * currently active file.
 */
button
  innerValue:@"<span class=""icon-link""></span>"
  title:Copy URL to current path
  disabled
  events

    /*
     * Sink invoked when active item in folder explorer has been changed.
     */
    hyper-ide.folder-explorer.item-changed

      /*
       * Checking if there is a selected item.
       */
      if:x:/../*/_arg?value

        /*
         * Enabling button.
         */
        delete-widget-property:x:/../*/_event?value
          disabled

      else

        /*
         * Disabling button since there is no active item in folder explorer.
         */
        set-widget-property:x:/../*/_event?value
          disabled

  onclick

    /*
     * Shows the URL for the current path to the user.
     */
    create-widget:hyper-ide-bookmark-url
      element:input
      type:text
      class:hyper-ide-bookmark-textbox
      onkeydown:@"if (event.keyCode == 13 || event.keyCode == 27) {p5.$('hyper-ide-bookmark-url').raise('.onclose');return false;}"
      onblur

        /*
         * Simply invoking [.onclose] when widget looses focus.
         */
        p5.web.widgets.ajax-events.raise:x:/../*/_event?value
          .onclose

      .onclose

        /*
         * Deleting widget.
         */
        delete-widget:x:/../*/_event?value

      oninit

        /*
         * Figuring out the URL for current path, and selecting all text.
         */
        hyper-ide.folder-explorer.get-active-item
        p5.web.get-location-url
        set-widget-property:x:/../*/_event?value
          value:{0}?path={1}
            :x:/@p5.web.get-location-url?value
            :x:/@hyper-ide.folder-explorer.get-active-item?value
        micro.page.set-focus:x:/../*/_event?value
