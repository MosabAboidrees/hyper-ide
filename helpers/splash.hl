/*
 * Initial "splash" screen.
 *
 * Welcomes user, and gives him some general feedback.
 */
container:hyper-ide-splash
  class:air-inner shaded rounded hyper-ide-splash
  oninit

    /*
     * Checking if user has a valid license.
     */
    if
      license.is-valid
      not

      /*
       * User does not have a valid license.
       *
       * Checking if "trial period" have been spent.
       * Notice, we don't bother the user for his first 7 days.
       */
      select-data:x:/*/*/hyper-ide.trial-date
      if:x:/@select-data/*/*?value
        not

        /*
         * Trial date has not been set, inserting it into p5.data.
         */
        p5.types.date.now
        eval-x:x:/+/*/*
        insert-data
          hyper-ide.trial-date
            date:x:/@p5.types.date.now?value

        /*
         * Returning early, to avoid displaying license info, since obviousy
         * trial period has not been exceeded, since there was no "trial-date"
         * in our p5.data database.
         */
        return

      else

        /*
         * Checking if "trial date" has expired.
         */
        p5.types.date.now
        -:x:/@p5.types.date.now?value
          _:x:/@select-data/*/*/date?value
        if:x:/@-?value
          <:time:"7.00:00:00.0000000"

          /*
           * Trial period has not yet ended.
           */
          return

      /*
       * If we came this far, then user does not have a valid license, and his
       * trial period has ended.
       *
       * Creating a modal window explaining the situation to the user, but making
       * sure we only do it once each session, since we don't want to bug user too
       * much, since he might just simply be creating Open Source software.
       */
      p5.web.session.get:hyper-ide.show-license-info
      if:x:/-/*?value

        /*
         * User has been warned earlier during his session.
         */
        return

      /*
       * OK, finally we can warn user about his licensing issues.
       */
      create-widgets
        micro.widgets.modal:hyper-ide-license-modal-window
          widgets
            h3
              innerValue:Get licensed
            img
              src:"https://phosphorusfive.files.wordpress.com/2017/09/license.jpg"
              style:"max-width:150px;float:left;box-shadow:3px 3px 5px rgba(0,0,0,.1);margin-right:1rem;border-radius:1rem;"
            p
              innerValue:@"We couldn't find a valid proprietary license in your installation. If you wish to create close source applications,
you'll probably need a valid proprietary license. Click the <em>'Get licensed'</em> button below to obtain one."
            p
              innerValue:@"A valid proprietary license will give you many benefits, such as getting rid of this warning, removing the promolink on your apps,
and give you access to professional support and additional learning material. In addition to that it allows you to create closed source applications using Phosphorus Five.
A license also makes you eligible for priority support."
            p
              innerValue:@"<strong>Warning</strong> - Without a proprietary license, you are not allowed to create closed source apps using Phosphorus Five, 
since Phosphorus Five is licensed as GPLv3."
            div
              class:right
              style:"clear:both;"
              widgets
                div
                  class:strip
                  widgets
                    button
                      innerValue:Get licensed
                      oninit

                        /*
                         * Setting initial focus to "Yes" button.
                         */
                        micro.page.set-focus:x:/../*/_event?value

                      onclick

                        /*
                         * Re-directing to "Bazar" to obtain a license.
                         */
                        p5.web.set-location:"/bazar?app=license"

                    button
                      innerValue:Nope
                      onclick

                        /*
                         * Simply deleting the modal window.
                         */
                        delete-widget:hyper-ide-license-modal-window

                        /*
                         * Making sure we don't ask the user again, during the current session.
                         */
                        p5.web.session.set:hyper-ide.show-license-info
                          src:bool:true
  widgets
    h1
      innerValue:Hyper IDE <small>{In the beginning there was Hyperlambda}</small>
    p
      widgets
        span
          innerValue:@"An ultra slick, super responsive, and highly modularised web based Integrated Development Environment, supporting 100+ programming languages out of the box.
Hyper IDE contains strong plugin support, in addition to several different types of template projects,
allowing you to literally start out your projects <em>'running'</em>."
        literal
          element:span
          oninit
            if
              license.is-valid
              set-widget-property:x:/../*/_event?value
                innerValue:You seem to be properly licensed, and ready to create proprietary applications if you wish.
            else
              set-widget-property:x:/../*/_event?value
                innerValue:@"Hyper IDE is Open Source, but there also exists proprietary licensing
options, which among other things makes you eligible for professional support, in addition to allowing you
to create close source apps in Hyperlambda. <a href=""/bazar?app=license"">Read more about the proprietary licensing options here</a>."

    p
      class:hyper-ide-copyright
      innerValue:@"Copyright Â© <a target=""_blank"" href=""https://gaiasoul.com"">Thomas Hansen</a>, thomas@gaiasoul.com, license GPLv3 ++"
    img
      src:"https://phosphorusfive.files.wordpress.com/2017/12/ufo.png"
      class:hyper-ide-ufo
      title:@"That feeling Hyper IDE gives you ..."
      alt:UFO

