
/*
 * This is our main "landing page" for the help system in Hyper IDE.
 *
 * This file displays the general "index", and such serves as a starting ground
 * for other files in the help system.
 */
h3
  innerValue:Help
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@IDE/helpers/help-files/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension.
       */
      split:x:/@_dp/#?name
        =:/
        =:.

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@split/0/-2?name
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              hyper-ide.help.display-file:x:/@get-widget-property/*/*?value
              
