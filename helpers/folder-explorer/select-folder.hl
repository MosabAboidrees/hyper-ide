/*
 * This file is invoked when a folder is selected in your folder explorer.
 *
 * The file expects a __[folder]__ argument being the path to the selected folder.
 * This file will create a toolbar allowing the user to edit the folder's properties,
 * such as renaming it, deleting it, etc - In addition to loading up all folder plugins,
 * and injecting these into your _"folder object toolbar"_.
 */





/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  folder:string





/*
 * Deleting any previously created edit active file object toolbars.
 */
if
  fetch:x:/0/0?value
    widget-exists:hyper-ide-active-folder-toolbar
  delete-widget:hyper-ide-active-folder-toolbar





/*
 * Hiding any previously shown edit file toolbars.
 */
p5.web.widgets.find-first-like
  .toolbar
  class:visible
micro.css.toggle:x:/-/*/*?value
  class:hide visible





/*
 * Adding all folder specific plugins.
 */
add:x:/../*/create-widgets/*/micro.widgets.file/*/widgets
  micro.evaluate.file:@IDE/helpers/get-plugins.hl
    type:hyper-ide.folder-plugin





/*
 * Making sure we create our active folder toolbar.
 */
create-widgets
  micro.widgets.file:hyper-ide-active-folder-toolbar
    folder:@IDE/helpers/toolbars/folder-editor/
    after:hyper-ide-toolbars
    .folder:x:/../*/folder?value
    class:strip right hyper-ide-toolbar
    widgets
