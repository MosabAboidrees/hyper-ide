/*
 * Template for "new module plugin" button.
 *
 * Creates a simple "Hello World" application.
 * Requires [name] as name of new module to create.
 */





/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  name:string
  database:string
  table:string





/*
 * Special settings for module.
 */
.settings
  micro.widgets.wizard-form
    select:hyper-ide-new-module-database-name
      info:Database
      .data-field:database
      onchange

        /*
         * Re-databinding tables select dropdown list.
         */
        hyper-ide.new-module.databind-tables

      oninit

        /*
         * Populating select dropdown with all databases that exists in MySQL.
         */
        p5.mysql.connect:[sys]
          p5.mysql.select:show databases

          /*
           * Iterating through each database, and creating one option element for each database.
           * Making sure we remove database prefix in the process.
           */
          p5.config.get:p5.data.prefix
          for-each:x:/@p5.mysql.select/*/*?value
            replace:x:/@_dp?value
              src:x:/@p5.config.get/*?value
            create-widget
              element:option
              parent:x:/../*/_event?value
              innerValue:x:/@replace?value

          /*
           * Making sure we databind our tables.
           */
          hyper-ide.new-module.databind-tables

    select:hyper-ide-new-module-database-table
      info:Table
      .data-field:table
      disabled
      events

        /*
         * Invoked when database name changes, selects all tables from currently selected
         * database from above select list, and creates one option element for each table.
         */
        hyper-ide.new-module.databind-tables

          /*
           * Figuring out database name, opening up database connection, and populates
           * select dropdown with all available tables from database.
           *
           * Making sure we delete any previously created option elements first.
           */
          clear-widget:hyper-ide-new-module-database-table
          get-widget-property:hyper-ide-new-module-database-name
            value
          delete-widget-property:x:/../*/_event?value
            disabled

          /*
           * Populating select dropdown with all databases that exists in MySQL.
           */
          p5.mysql.connect:[{0}]
            :x:/@get-widget-property/*/*?value
            p5.mysql.select:show tables

            /*
             * Iterating through each database, and creating one option element for each table.
             */
            for-each:x:/@p5.mysql.select/*/*?value
              create-widget
                element:option
                parent:x:/../*/_event?value
                innerValue:x:/@_dp?value





/*
 * Creates our new module.
 *
 * First making sure our main module folder exists.
 */
create-folder:/modules/{0}/
  :x:/../*/name?value





/*
 * Then creating our module's files, first the "launch.hl" file.
 */
save-file:/modules/{0}/launch.hl
  :x:/../*/name?value
  src:@"
/*
 * Includes CSS for our module.
 */
micro.page.minify-and-bundle-css-file
  @MICRO/media/main.css
  @MICRO/media/fonts.css
  @MICRO/media/skins/serious.css
p5.web.include-css-file:x:/-?value





/*
 * Change this to specifically named columns, if you don't want to show all columns.
 */
.columns:*





/*
 * Setting page title.
 */
p5.web.page.set-title:{0}.{1} items





/*
 * Creating our columns in our grid below
 */
p5.mysql.connect:[{0}]
  p5.mysql.select:select {{0}} from `{1}` limit 1
    :x:/@.columns?value
  for-each:x:/@p5.mysql.select/*/*?name
    add:x:/../*/create-widget/**/micro.widgets.grid/*/columns
      src:x:/@_dp?value





/*
 * Creating main wire frame for module.
 */
eval-x:x:/+/**/.columns
create-widget
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:right
              widgets
                div
                  class:strip toolbar
                  style:""display:inline-block;""
                  widgets
                    button
                      innerValue:@""<span class=""""icon-home3""""></span>""
                      onclick

                        /*
                         * Redirecting user to server's root URL.
                         */
                        p5.web.get-root-location
                        p5.web.set-location:x:/-?value
        div
          class:col-100
          widgets
            div
              class:air-inner shaded rounded bg
              widgets
                h3
                  innerValue:DataGrid
                micro.widgets.grid:app-template-grid
                  class:hover striped
                  columns
                  oninit

                    /*
                     * Connects to database, selects first 10 records, 
                     * and creates a datagrid item for each.
                     */
                    .columns:x:/../*/.columns?value
                    p5.mysql.connect:[{0}]
                      p5.mysql.select:select {{0}} from `{1}` limit 10
                        :x:/@.columns?value

                      /*
                       * Looping through each row, adding one [item] for each.
                       */
                      for-each:x:/@p5.mysql.select/*
                        add:x:/../*/micro.widgets.grid.databind
                          src:item

                        /*
                         * Looping through each cell in row, creating one databound value for each.
                         */
                        for-each:x:/@_dp/#/*
                          add:x:/../*/micro.widgets.grid.databind/0/-
                            src:@""{{0}}:""""{{1}}""""""
                              :x:/@_dp/#?name
                              :x:/@_dp/#?value

                    /*
                     * Databinding our grid.
                     */
                    micro.widgets.grid.databind::x:/../*/_event?value
"
    :x:/../*/database?value
    :x:/../*/table?value





/*
 * Then creating our "desktop.hl" file.
 */
save-file:/modules/{0}/desktop.hl
  :x:/../*/name?value
  src:@"
/*
 * Desktop widget.
 *
 * This becomes your module's 'desktop icon'.
 */
container
  element:a
  class:desktop-app shaded rounded air-inner
  title:This module says Hello World!
  widgets
    span
      class:desktop-app-name
      widgets
        literal
          element:span
          class:icon-happy desktop-app-icon
        span
          class:desktop-app-text
          innerValue:Datagrid
"





/*
 * Then creating our "startup.hl" file.
 */
to-upper:x:/../*/name?value
save-file:/modules/{0}/startup.hl
  :x:/../*/name?value
  src:@"
/*
 * Provide whatever startup logic your module requires here, if any.
 *
 * For instance, create Active Events necessary to wire up your module.
 * Below is an example of creating a 'path' event for instance, which unrolls
 * the path declaration of @HELLO to your module's main folder.
 */
split:x:/..?name
  =:/
set:x:/@split/0/-
join:x:/@split/*?name
  sep:/
eval-x:x:/+/*
create-event:p5.io.unroll-path.@{0}
  return:/{{0}}
    :x:/@join?value
"
    :x:/@to-upper?value





/*
 * Then creating our "uninstall.hl" file.
 */
save-file:/modules/{0}/uninstall.hl
  :x:/../*/name?value
  src:@"
/*
 * Provide whatever uninstall logic your module requires here, if any.
 *
 * Common tasks for this file, is to delete all Active Events created by
 * your module, such as the following example illustrates.
 */
delete-event:p5.io.unroll-path.@{0}
"
    :x:/@to-upper?value
