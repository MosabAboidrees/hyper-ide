
/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  editor:string





/*
 * Checking if specified [editor] argument is a filepath, and if not, making
 * sure we turn it into a filepath.
 */
if
  starts-with:x:/../*/editor?value
    src:/
  not

  /*
   * Specified [editor] argument is not a filepath, assuming it's the ID of our
   * CodeMirror editor, hence retrieving CodeMirror editor's filepath, which can
   * be found in CodeMirror editor's parent widgets [.editor] attribute.
   */
  get-widget-property:x:/../*/editor?value
    .editor

  /*
   * Updating [editor] argument to reflect filepath of editor caller
   * wants to activate.
   */
  set:x:/../*/editor?value
    src:x:/@get-widget-property/*/*?value





/*
 * Checking if [editor] is already active, at which point we simply return early.
 */
if
  hyper-ide.active-editor.get-filepath
  =:x:/../*/editor?value

  /*
   * Nothing to do here, simply returning early, warning user first though.
   */
  micro.windows.info:Editor is already active
    class:micro-windows-info warning
  return





/*
 * Retrieving activate button for editor.
 */
p5.web.widgets.find-first:hyper-ide-editor-tab-editors
  .editor:x:/../*/editor?value





/*
 * Verifying specified [editor] exists.
 */
if:x:/@p5.web.widgets.find-first/*/*?value

  /*
   * Editor passed in as [editor] was found.
   *
   * For simplicity reasons, we simply select tree view item associated
   * with editor, since this will map up everything automatically for us.
   */
  add:x:/+/*/items
    src:x:/../*/editor?value
  micro.widgets.tree.select-items:hyper-ide-folder-tree-browser
    items

  /*
   * Making sure we scroll our selected tree view item into view.
   */
  p5.web.widgets.find-first-like:hyper-ide-folder-tree-browser
    class:tree-selected
  p5.web.send-javascript:@"p5.$('{0}').el.scrollIntoView ({{behavior:'smooth'}});"
    :x:/@p5.web.widgets.find-first-like/*/*?value

  /*
   * Returning success to caller.
   */
  return:bool:true

else

  /*
   * No such editor.
   */
  micro.windows.info:No such editor
    class:micro-windows-info warning

  /*
   * Returning failure to caller.
   */
  return:bool:false
