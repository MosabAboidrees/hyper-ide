
/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  editor:string





/*
 * Retrieving all open editors.
 */
hyper-ide.editors.get-open-editors





/*
 * Checking if specified [editor] argument is a filepath.
 */
if
  starts-with:x:/../*/editor?value
    src:/
  not

  /*
   * Specified [editor] argument is not a filepath, assuming it's the ID of our
   * CodeMirror editor, hence retrieving CodeMirror editor's filepath, which can
   * be found in CodeMirror editor's parent widgets [.editor] attribute.
   */
  p5.web.widgets.get-parent:x:/../*/editor?value
  get-widget-property:x:/-/*/*?value
    .editor

  /*
   * Updating [editor] argument to reflect filepath of editor caller
   * wants to activate.
   */
  set:x:/../*/editor?value
    src:x:/@get-widget-property/*/*?value





/*
 * Verifying specified [editor] is found amongst open editors.
 */
if:x:@"/@hyper-ide.editors.get-open-editors/*/""{0}"""
  :x:/../*/editor?value

  /*
   * Editor passed in as [editor] was found.
   *
   * For simplicity reasons, we simply select tree view item associated
   * with editor, since this will map up everything automatically for us.
   */
  add:x:/+/*/items
    src:x:/../*/editor?value
  micro.widgets.tree.select-items:hyper-ide-folder-tree-browser
    items

  /*
   * Making sure we scroll our selected tree view item into view.
   */
  p5.web.widgets.find-first-like:hyper-ide-folder-tree-browser
    class:tree-selected
  p5.web.send-javascript:@"p5.$('{0}').el.scrollIntoView ({{behavior:'smooth'}});"
    :x:/@p5.web.widgets.find-first-like/*/*?value

  /*
   * Returning true to caller, indicating that we had success in setting
   * the current editor.
   */
  return:bool:true
