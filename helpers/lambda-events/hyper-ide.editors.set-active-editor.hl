/*
 * This file sets the active editor to the given **[editor]** value, which can either
 * be the path to an already open editor, or a CodeMirror widget wrapper ID for an editor
 * that is already open.
 */

/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  editor:string





/*
 * Checking if specified [editor] argument is a filepath, and if not, assuming
 * it's the ID to its CodeMirror editor, and making sure we turn it into a filepath.
 */
if
  starts-with:x:/../*/editor?value
    src:/
  not

  /*
   * Specified [editor] argument is not a filepath, assuming it's the ID of our
   * CodeMirror editor, hence retrieving CodeMirror editor's filepath, which can
   * be found in CodeMirror editor's parent widgets [.editor] attribute.
   */
  p5.web.widgets.get-parent:x:/../*/editor?value
  get-widget-property:x:/@p5.web.widgets.get-parent/*/*?value
    .editor

  /*
   * Updating [editor] argument to reflect filepath of editor caller
   * wants to activate.
   */
  set:x:/../*/editor?value
    src:x:/@get-widget-property/*/*?value





/*
 * Checking if editor caller wants to activate is already the active editor, at
 * which point we don't go through the entire activation process, but only a part of it.
 */
if
  hyper-ide.active-editor.get-filepath
  =:x:/../*/editor?value

  /*
   * Notice, we still need to make sure the toolbar is the currently active toolbar.
   */
  p5.web.widgets.find-first:hyper-ide-toolbar-wrapper
    .toolbar:x:/../*/editor?value
  get-widget-property:x:/@p5.web.widgets.find-first/*/*?value
    class
  if:x:/@get-widget-property/*/*?value
    !~:visible

    /*
     * Need to activate editor's toolbar.
     */
    p5.web.widgets.find-first-like:hyper-ide-toolbar-wrapper
      class:visible

    /*
     * Finding any previously activated toolbars, and hiding them.
     */
    if:x:/@p5.web.widgets.find-first-like/*/*?value
      micro.css.toggle:x:/@p5.web.widgets.find-first-like/*/*?value
        class:visible hide

    /*
     * Displaying current file's toolbar.
     */
    micro.css.toggle:x:/@p5.web.widgets.find-first/*/*?value
      class:visible hide

    /*
     * Checking if there's a folder toolbar open, and if so, deleting it.
     */
    if
      fetch:x:/0/0?value
        widget-exists:hyper-ide-active-folder-toolbar
      delete-widget:hyper-ide-active-folder-toolbar

  /*
   * Returning early.
   */
  return:bool:true





/*
 * Retrieving editor wrapper widget for requested editor.
 */
p5.web.widgets.find-first:hyper-ide-editor-tab-editors
  .editor:x:/../*/editor?value





/*
 * Verifying specified [editor] exists.
 */
if:x:/@p5.web.widgets.find-first/*/*?value

  /*
   * Editor passed in as [editor] was found.
   *
   * For simplicity reasons, we simply select tree view item associated
   * with editor, since this will map up everything automatically for us.
   */
  add:x:/+/*/items
    src:x:/../*/editor?value
  micro.widgets.tree.select-items:hyper-ide-folder-tree-browser
    items

  /*
   * Raising our folder changed event.
   */
  hyper-ide.file-explorer.item-changed:x:/../*/editor?value

  /*
   * Returning success to caller.
   */
  return:bool:true

else

  /*
   * No such editor.
   */
  micro.windows.info:No such open editor
    class:micro-windows-info warning

  /*
   * Returning failure to caller.
   */
  return:bool:false
