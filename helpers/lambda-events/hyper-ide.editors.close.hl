/*
 * Closes all editors who's paths starts with the given **[filter]**, unless they
 * have unsaved changes. If you want to force closing also editors with changes,
 * you can supply a **[force]** argument, and set its value to boolean _"true"_.
 *
 * If you provide a boolean true value for **[exact]**, only the editor being
 * an exact match will be closed.
 */

/*
 * Separates arguments from the rest of our lambda.
 */
.signal





/*
 * Sanity checking arguments.
 */
micro.lambda.contract.optional:x:/..
  exact:bool
  filter:string
  force:bool





/*
 * Defaults.
 */
.defaults
  filter:/





/*
 * Retrieving all open editors, and checking that there actually are any
 * open editors, and if not, returning early.
 */
hyper-ide.editors.get-open-editors
if:x:/@hyper-ide.editors.get-open-editors/*?count
  =:int:0

  /*
   * No open editors, returning early to avoid evaluating the rest of our lambda.
   */
  micro.windows.info:There are no open editors
    class:micro-windows-info warning
  return:bool:false





/*
 * Retrieving active editor, since we'll need to track if it was closed or
 * not during our lambda evaluation.
 */
hyper-ide.active-editor.get-filepath





/*
 * Used to track dirty editors we are not closing.
 */
.dirty





/*
 * Used to track if active editor is closed.
 */
_active-deleted:bool:false





/*
 * Iterating through all open editors.
 */
for-each:x:/@hyper-ide.editors.get-open-editors/*

  /*
   * Checking if currently iterated editor is a match for our [filter] condition.
   */
  if:x:/@_dp/#?name
    =:x:(/../*/filter|/@.defaults/*/filter)/$?value
    or:x:/../*/exact?value.bool
      !=:bool:true
      and
        starts-with:x:/@_dp/#?name
          src:x:(/../*/filter|/@.defaults/*/filter)/$?value

    /*
     * Checking if currently iterated editor is dirty, and [force] is not true.
     */
    if:x:/@_dp/#/*/clean?value
      =:bool:false
      and:x:/../*/force?value.bool
        !=:bool:true

      /*
       * Currently iterated editor is dirty, and user did not want to [force] closing,
       * hence we do not close it, but store the editor in our [.dirty] list, and
       * continue our iteration.
       */
      add:x:/@.dirty
        src:x:/@_dp/#?name
      continue

    /*
     * Checking if this was our active editor.
     */
    if:x:/@_dp/#?name
      =:x:/@hyper-ide.active-editor.get-filepath?value

      /*
       * This is our active editor, making sure we track the fact that it
       * was closed.
       */
      set:x:/@_active-deleted?value
        src:bool:true

    /*
     * This is where the actual deletion of our editor is done.
     *
     * We'll need to delete the editor's toolbar, activate tab view button,
     * and actual editor.
     */

    /*
     * Deleting actual editor.
     */
    p5.web.widgets.find-first:hyper-ide-editor-tab-editors
      .editor:x:/@_dp/#?name
    delete-widget:x:/@p5.web.widgets.find-first/*/*?value

    /*
     * Then its activate button.
     */
    p5.web.widgets.find-first:hyper-ide-editor-tab-buttons
      .activate:x:/@_dp/#?name
    delete-widget:x:/@p5.web.widgets.find-first/*/*?value

    /*
     * Then its toolbar.
     */
    p5.web.widgets.find-first
      .toolbar:x:/@_dp/#?name
    delete-widget:x:/@p5.web.widgets.find-first/*/*?value

    /*
     * Removing currently iterated editor from our list of editors.
     *
     * This is done such that we end up with a list of available editors,
     * after we have closed all editors caller wants to close, which (might) be
     * used to determine which editor we should activate, if any.
     */
    set:x:/@_dp/#





/*
 * Checking if there was dirty editors, that were not closed, and if so,
 * showing a modal widget, warning user, asking him if he really wants to
 * close these editors, setting the last dirty editor to active, and returning
 * early to avoid evaluation of the rest of our lambda.
 */
if:x:/@.dirty/*

  /*
   * Dirty editors exists, and they were not closed, even though caller
   * requested to have these editors closed.
   *
   * Hence, we display a modal window to caller, allowing him to force closing,
   * sets the last dirty editor to active, and return early, to avoid evaluating
   * the rest of our lambda.
   *
   * First passing in all arguments to our "recursive" invocation, to make sure
   * we get our [exact] and [filter] arguments correctly mapped up in our second
   * invocation.
   */
  add:x:/+/**/hyper-ide.editors.close
    src:x:/@.signal/--/<-
  micro.windows.confirm
    header:You have unsaved changes
    body:Some of your editors have unsaved changes, are you sure you want to close these? If you do, your changes will not be saved.
    onok

      /*
       * Invoking "self" with [force] argument, deleting modal widget first.
       */
      hyper-ide.editors.close
        force:bool:true

  /*
   * Checking if we need to activate our last dirty editor.
   */
  if:x:/@hyper-ide.active-editor.get-filepath?value
    !=:x:/@.dirty/0/-?name

    /*
     * We need to set our last dirty editor to active.
     */
    hyper-ide.editors.set-active-editor:x:/@.dirty/0/-?name

  /*
   * Returning "failure" to caller.
   */
  return:bool:false





/*
 * Checking if active editor was deleted, and if it was, we'll need to
 * either load up another editor, or load our splash screen, depending 
 * upon whether or not there are anymore available editors.
 */
if:x:/@_active-deleted?value

  /*
   * Active editor was closed.
   *
   * Checking if there are anymore editors left.
   */
  if:x:/@hyper-ide.editors.get-open-editors/*?count
    >:int:0

    /*
     * Setting the last available editor as our active editor.
     */
    hyper-ide.editors.set-active-editor:x:/@hyper-ide.editors.get-open-editors/0/-?name

  else

    /*
     * No more editors, deleting editor wrapper, and loading our splash screen.
     */
    delete-widget:hyper-ide-editor-tab
    hyper-ide._load-splash

    /*
     * Raising our "all editors closed" event.
     */
    hyper-ide.on-all-editors-closed

    /*
     * Checking if active item in folder explorer is a file, and if it is, we make
     * sure we de-select it.
     */
    hyper-ide.folder-explorer.get-active-item
    if
      ends-with:x:/@hyper-ide.folder-explorer.get-active-item?value
        src:/
      not

      /*
       * Active folder explorer item is a file, hence we de-select it.
       */
      hyper-ide.folder-explorer.set-active-item





/*
 * Returning "success" to caller.
 */
return:bool:true
