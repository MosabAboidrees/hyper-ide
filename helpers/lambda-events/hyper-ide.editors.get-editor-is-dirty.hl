
/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  editor:string





/*
 * Checking if supplied [editor] argument was a filepath, at which point we
 * change it to become its widget ID.
 */
if
  starts-with:x:/../*/editor?value
    src:/

  /*
   * Specified [editor] argument was a filepath argument, changing it to become
   * editor's ID instead.
   */
  p5.web.widgets.find-first:hyper-ide-editor-tab-editors
    .editor:x:/../*/editor?value
  set:x:/../*/editor?value
    src:x:/@p5.web.widgets.find-first/*/*?value





/*
 * Finding dirty input element for editor.
 */
p5.web.widgets.find-first:x:/../*/editor?value
  element:input
  type:hidden





/*
 * Verifying specified [editor] argument actually was an editor.
 */
if:x:/@p5.web.widgets.find-first/*/*?value
  not

  /*
   * Specified [editor] argument was not a CodeMirror editor.
   */
  micro.windows.info:No such editor
    class:micro-windows-info warning
  return





/*
 * Getting value of dirty input element.
 */
get-widget-property:x:/@p5.web.widgets.find-first/*/*?value
  value
if:x:/-/*/*?value
  =:dirty

  /*
   * Active editor is dirty.
   */
  return:bool:true

/*
 * Active editor is not dirty.
 */
return:bool:false
