/*
 * Guides user through process of creating a new skin.
 *
 * Uses a "wizard" form to help user through the process.
 * First loading main Micro CSS file, to determine all CSS variables in file.
 */
load-file:@MICRO/media/micro.css





/*
 * Finding all CSS variables that are available to us.
 */
split:x:/@load-file/*?value
  =:"/* VARIABLES_BEGIN */"
  =:"/* VARIABLES_END */"
split:x:/@split/1?name
  =:"\r\n"
.vars
for-each:x:/@split/*?name
  trim:x:/@_dp?value
    chars:"\t -;"
  split:x:/@trim?value
    =:":"
  trim:x:/@split/1?name
    chars:"; \t\r\n"
  add:x:/@.vars
    src:"{0}:{1}"
      :x:/@split/0?name
      :x:/@trim?value
set:x:/@.vars/*/=




/*
 * Applying all CSS variables into our tab widget.
 */
.no:int:1
while:x:/@.vars/*
  eval-x:x:/+/*/*/*/name
  add:x:/../*/create-widgets/*/*/*/micro.widgets.tab
    src
      view
        name:Settings {0}
          :x:/@.no?value
        widgets
  apply:x:/../*/create-widgets/*/*/widgets/*/micro.widgets.tab/0/-/*/widgets
    src:x:/@.vars/*/[0,7]
    template
      div
        class:strip fill
        widgets
          label
            class:description-9
            {innerValue}:x:?name
          input
            type:text
            {value}:x:?value
            {.data-field}:x:?name
  set:x:/@.no?value
    +:x:/@.no?value
      _:1
  set:x:/@.vars/*/[0,7]





/*
 * Adding the "preview" view.
 */
add:x:/../*/create-widgets/**/micro.widgets.tab
  src
    view
      name:Preview
      widgets
        div
          oninit

            /*
             * Injecting a style tag overriding the default skin
             * with whatever settings user supplied.
             *
             * First creating our skin's CSS.
             */
            micro.form.serialize:hyper-ide-create-skin-wizard-modal
            .css:@"<style>
.hyper-ide-preview-skin {"
            for-each:x:/@micro.form.serialize/*
              set:x:/@.css?value
                src:"{0}--{1}:{2};"
                  :x:/@.css?value
                  :x:/@_dp/#?name
                  :x:/@_dp/#?value
            set:x:/@.css?value
              src:"{0}}}</style>"
                :x:/@.css?value
            create-widgets
              text:x:/@.css?value
                parent:x:/../*/_event?value


          widgets

            /*
             * Then illustrating samples of most of our widgets, to
             * simply show off how the skin will look like.
             */
            div:hyper-ide-preview-widget
              class:hyper-ide-preview-skin background air-left air-right air-bottom air-inner
              widgets
                h2
                  innerValue:Header H2
                p
                  innerValue:@"Some paragraph with <code>inline code</code> and
a <a href='https://gaiasoul.com'>hyperlink</a> and a <strong>strong</strong> and <em>emphasized</em> string."
                pre
                  innerValue:@"Some pre formatted
  piece of text"
                div
                  class:strip fill
                  widgets
                    label
                      innerValue:Label
                    input
                      type:text
                      placeholder:Some piece of input ...
                    label
                      widgets
                        span
                          innerValue:Check
                        input
                          type:checkbox
                div
                  class:strip fill
                  widgets
                    label
                      innerValue:Disabled
                    input
                      type:text
                      disabled
                      value:Some piece of input
                    label
                      widgets
                        span
                          innerValue:Check
                        input
                          disabled
                          checked
                          type:checkbox
                label
                  widgets
                    span
                      innerValue:Radio button 1
                    input
                      type:radio
                      name:hyper-ide-preview-rdo
                label
                  widgets
                    span
                      innerValue:Radio button 2
                    input
                      type:radio
                      name:hyper-ide-preview-rdo
                label
                  widgets
                    span
                      innerValue:Disabled radio button
                    input
                      type:radio
                      disabled
                      name:hyper-ide-preview-rdo
                div
                  class:shaded rounded air-inner bg
                  innerValue:<p>Shaded and rounded div with bg</p>
                div
                  class:air-inner success
                  innerValue:<p>Success div</p>
                div
                  class:warning air-inner
                  innerValue:<p>Warning div</p>
                button
                  class:air-right
                  innerValue:Some button
                button
                  class:toggled air-right
                  innerValue:Toggled button
                button
                  disabled
                  innerValue:Disabled button





/*
 * Creating a modal widget, with one textbox for each variable.
 */
create-widgets
  micro.widgets.modal:hyper-ide-create-skin-wizard-modal
    class:micro-widgets-modal large
    widgets
      h3
        innerValue:Skin wizard
      micro.widgets.tab
      div
        class:strip right air-top
        widgets
          button
            innerValue:Generate
            onclick

              /*
               * Creating (another) modal widget to ask user for a name for his new skin.
               */
              create-widgets
                micro.widgets.modal:hyper-ide-create-skin-wizard-modal-name
                  widgets
                    h3
                      innerValue:Provide a name for your skin
                    div
                      class:fill strip
                      widgets
                        label
                          innerValue:Name
                        input:hyper-ide-new-skin-name
                          type:text
                          placeholder:Name for your skin ...
                          onkeydown:@"if (event.keyCode == 13) {p5.$('hyper-ide-save-new-skin').raise('onclick');return false;} else if (event.keyCode == 27) {p5.$('hyper-ide-save-new-skin-cancel').raise('onclick');return false;}"
                          oninit

                            /*
                             * Setting initial focus to textbox.
                             */
                            micro.page.set-focus:x:/../*/_event?value

                    div
                      class:right strip
                      widgets
                        button:hyper-ide-save-new-skin
                          innerValue:Save
                          onclick

                            /*
                             * Serializing form and saving to a skin file.
                             *
                             * Notice, to avoid serializing preview widgets, we delete them first.
                             *
                             */
                            delete-widget:hyper-ide-preview-widget
                            micro.form.serialize:hyper-ide-create-skin-wizard-modal

                            /*
                             * Building CSS.
                             */
                            .css:@"/* CSS variables */
:root {"
                            for-each:x:/@micro.form.serialize/*
                              set:x:/@.css?value
                                src:"{0}--{1}:{2};"
                                  :x:/@.css?value
                                  :x:/@_dp/#?name
                                  :x:/@_dp/#?value
                            set:x:/@.css?value
                              src:"{0}}}"
                                :x:/@.css?value

                            /*
                             * Retrieving and sanity checking filename.
                             */
                            get-widget-property:hyper-ide-new-skin-name
                              value
                            match:x:/@get-widget-property/*/*?value
                              src:regex:/^[-_a-z0-9]+$/i
                            if:x:/@match/*?count
                              =:int:0
                              or:x:/@match/0?name
                                =:

                              /*
                               * Not a legal name for a file.
                               */
                              micro.windows.info:Not a legal name for your skin
                                class:micro-windows-info warning
                              return

                            /*
                             * Saving skin file.
                             */
                            save-file:@MICRO/media/skins/{0}.css
                              :x:/@get-widget-property/*/*?value
                              src:x:/@.css?value

                            /*
                             * Deleting both modal widgets.
                             */
                            delete-widget:hyper-ide-create-skin-wizard-modal-name
                            delete-widget:hyper-ide-create-skin-wizard-modal

                            /*
                             * Refreshing tree view's active folder, which should be the skin folder.
                             */
                            hyper-ide.folder-explorer.refresh-active-folder

                            /*
                             * Opening up our CSS file for editing.
                             */
                            hyper-ide.folder-explorer.select-path:/modules/micro/media/skins/{0}.css
                              :x:/@get-widget-property/*/*?value

                        button:hyper-ide-save-new-skin-cancel
                          innerValue:Cancel
                          onclick

                            /*
                             * Simply deleting modal widget.
                             */
                            delete-widget:hyper-ide-create-skin-wizard-modal-name

          button
            innerValue:Cancel
            onclick

              /*
               * Simply deleting modal widget.
               */
              delete-widget:hyper-ide-create-skin-wizard-modal
