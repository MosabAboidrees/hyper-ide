
/*
 * Edits the specified [file], using some sort of CodeMirror editor, or whatever
 * is available.
 */





/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  file:string





/*
 * Deleting any previously created edit active file object toolbars.
 */
if
  fetch:x:/0/0?value
    widget-exists:hyper-ide-active-file-object-toolbar

  /*
   * A folder is currently edited, deleting active file object toolbar for
   * the folder that is edited.
   */
  delete-widget:hyper-ide-active-file-object-toolbar





/*
 * Checking if file is already open, at which point we simply activate existing
 * editor.
 */
p5.web.widgets.find:hyper-ide-editor-tab-buttons
  .activate:x:/../*/file?value
if:x:/@p5.web.widgets.find/*/*?value

  /*
   * Editor for file is already open, activating editor, and returning early.
   */
  eval-x:x:/+/*
  micro.evaluate.file:@IDE/helpers/activate-editor.hl
    file:x:/../*/file?value

  /*
   * Returning early to abort evaluation of the rest of our lambda (file).
   */
  return





/*
 * Hiding any previously shown edit file toolbars.
 */
p5.web.widgets.find-first-like:hyper-ide-toolbar-wrapper
  .toolbar
  class:visible
micro.css.toggle:x:/@p5.web.widgets.find-first-like/*/*?value
  class:hide visible





/*
 * Making sure we load correct editor according to file extension.
 *
 * First loading file extension to CM mode mapping file.
 * This file contains a bunch of file extensions, that are "mapped" towards
 * CodeMirror modes.
 *
 * Hence, we load the file, do a loopkup for our file extension, at which point
 * we end up with a CodeMirror mode.
 *
 * First we retrieve file extension of file, and load our CodeMirror mapping file.
 */
split:x:/../*/file?value
  =:.
load-file:@IDE/configuration/extension2cm-instance.hl





/*
 * Checking if mode exists for file extension.
 */
if:x:/@load-file/*/*/{0}
  :x:/@split/0/-?name

  /*
   * Loading file, such that we can pass its content into our editor instance.
   *
   * Making sure we do not convert the file.
   */
  load-file:x:/../*/file?value
    convert:false

  /*
   * CodeMirror mode for file's extension exists.
   *
   * Adding CodeMirror editor with correct mode to our [widgets] collection below.
   */
  eval-x:x:/+/*/*/*
  add:x:/../*/create-widgets/*/div/*/widgets/=editor
    src
      micro.widgets.codemirror
        .data-field:content
        mode:x:/@load-file/@load-file/*/*/{0}?value
          :x:/@split/0/-?name
        value:x:/@load-file/*?value
        auto-focus:true
        keys
          Alt-S:p5.$('hyper-ide-main-container').raise('.onsave');
          Alt-X:p5.$('hyper-ide-main-container').raise('.onclose');
          Alt-W:p5.$('hyper-ide-main-container').raise('.onnext');
          Alt-Q:p5.$('hyper-ide-main-container').raise('.onprevious');

else

  /*
   * No editor exists, returning early.
   */
  return





/*
 * Checking if splash screen is open, and closing it if it is.
 */
if
  fetch:x:/0/0?value
    widget-exists:hyper-ide-splash
  delete-widget:hyper-ide-splash





/*
 * Making sure our "tab control" exists.
 *
 * These widgets will "wrap" all of our editors.
 */
if
  fetch:x:/0/0?value
    widget-exists:hyper-ide-editor-tab
  not

  /*
   * Creating our "tab control" wrapper.
   */
  create-widget:hyper-ide-editor-tab
    parent:hyper-ide-file-editor
    class:hyper-ide-editor-tab
    widgets
      container:hyper-ide-editor-tab-buttons
        class:hyper-ide-editor-tab-buttons
      container:hyper-ide-editor-tab-editors
        class:hyper-ide-editor-tab-content shaded rounded





/*
 * Making sure we hide any previously created file toolbars.
 *
 * This is the toolbar that contains "Save" button, "Close" button, and plugin
 * buttons, etc.
 */
p5.web.widgets.find-first-like
  .toolbar
  class:visible
micro.css.toggle:x:/@p5.web.widgets.find-first-like/*/*?value
  class:hide visible





/*
 * Making sure we hide any previously created editors, in addition to disabling
 * their textarea to preserve bandwidth.
 */
p5.web.widgets.find-first-like:hyper-ide-file-editor
  .editor
  class:visible
micro.css.toggle:x:/@p5.web.widgets.find-first-like/*/*?value
  class:hide visible





/*
 * Notice, to prevent editor's content being serialised back from client upon
 * consecutive Ajax requests, we disable the editor's textarea
 */
p5.web.widgets.find-first:x:/@p5.web.widgets.find-first-like/*/*?value
  element:textarea
set-widget-property:x:/@p5.web.widgets.find-first/*/*?value
  disabled





/*
 * "Un-toggling" all previously created tab buttons.
 */
p5.web.widgets.find-first-like:hyper-ide-editor-tab-buttons
  .activate
  class:toggled
micro.css.toggle:x:/-/*/*?value
  class:toggled





/*
 * Adding all file specific plugins.
 */
insert-before:x:/../*/create-widgets/*/div/[0,1]/*/widgets/0/-
  eval-x:x:/+/*
  micro.evaluate.file:@IDE/helpers/get-plugins.hl
    type:hyper-ide.plugins.{0}
      :x:/@load-file/*/*/{0}
        :x:/@split/0/-?name





/*
 * Helper JavaScript to make sure switching between tabs "feels" faster.
 *
 * Basically switches immediately, by updating CSS classes of active and to-become
 * active editor, before invoking server-side method to do "hard work".
 */
p5.web.include-javascript:@"
p5.hyper_ide_show_editor = function (ed) {
  ed = p5.$(ed).el;
  ed.className = 'toggled';
  var all = document.querySelectorAll('[data-editor]');
  var file = ed.getAttribute('data-activate');
  var cur = document.querySelectorAll('[data-editor=""' + file + '""]')[0];
  for(var idx = 0; idx < all.length; idx++) {{
    if (all[idx].id == ed.id) {{
      continue;
    }}
    all[idx].className = 'hyper-ide-codemirror-wrapper hide';
  }}
  cur.className = 'hyper-ide-codemirror-wrapper visible';
  p5.$(ed.id).raise('.onclick');
  return false;
}"





/*
 * Making sure we enable our "Close all editors" button.
 */
delete-widget-property:hyper-ide-close-all-editors
  disabled





/*
 * Figuring out filename of file, without its folder.
 */
split:x:/../*/file?value
  =:/





/*
 * Creating our widgets.
 *
 * We create one "tab control button", then we add one editor into "tab control editors",
 * before we finally create one toolbar for saving file, etc. The latter will also contain
 * all plugins for current mode.
 */
create-widgets

  /*
   * Toolbar for saving editor's content, closing it, etc.
   */
  div
    after:hyper-ide-toolbars
    class:strip right hyper-ide-toolbar visible
    .toolbar:x:/../*/file?value
    events

      /*
       * Invoked when for some reasons active file changes name.
       */
      hyper-ide.set-active-filename

        /*
         * Checking if this is our active editor, and if not, returning early.
         */
        get-widget-property:x:/../*/_event?value
          class
        if:x:/@get-widget-property/*/*?value
          ~:hide
          return

        /*
         * Updating [.file] attribute for widget.
         */
        set-widget-property:x:/../*/_event?value
          .toolbar:x:/../*/_arg?value

    widgets

      /*
       * Save button.
       */
      button
        innerValue:@"<span class=""icon-floppy-disk""></span>"
        title:Saves your file, keyboard shortcut Alt+S
        .save-button:x:/../*/file?value
        onclick

          /*
           * Invoking event responsible for saving file.
           */
          hyper-ide.active-editor.save

      /*
       * Renames file.
       */
      button
        innerValue:@"<span class=""icon-pencil""></span>"
        title:Renames your file
        onclick

          /*
           * Renaming file.
           * First retrieving file name.
           */
          p5.web.widgets.get-parent:x:/../*/_event?value
          get-widget-property:x:/-/*/*?value
            .toolbar

          /*
           * Then passing filename into modal widget.
           */
          eval-x:x:/+/**/.filename
          create-widgets
            micro.widgets.modal:hyper-ide-confirm-rename-file
              widgets
                h3
                  innerValue:New name
                micro.widgets.wizard-form:hyper-ide-rename-file-form
                  text:hyper-ide-new-file-name
                    info:Name
                    .data-field:name
                    placeholder:New name for your file ...
                    onkeydown:@"if (event.keyCode == 13) {p5.$('hyper-ide-rename-file-btn').raise('onclick');return false;}"
                    oninit

                      /*
                       * Forward evaluated above.
                       */
                      .filename:x:/../*/get-widget-property/*/*?value
                      split:x:/-?value
                        =:/

                      /*
                       * Setting initial focus to "Yes" button, and setting 
                       * initial value to old name.
                       */
                      set-widget-property:x:/../*/_event?value
                        value:x:/@split/0/-?name
                      micro.page.set-focus:x:/../*/_event?value

                div
                  class:right
                  widgets
                    div
                      class:strip
                      widgets
                        button:hyper-ide-rename-file-btn
                          innerValue:OK
                          onclick

                            /*
                             * Forward evaluated above.
                             */
                            .filename:x:/../*/get-widget-property/*/*?value

                            /*
                             * Finding file's folder name.
                             *
                             * Making sure we get it in canonised form.
                             */
                            .folder
                            split:x:/@.filename?value
                              =:/
                            set:x:/@split/0/-
                            join:x:/@split/*?name
                              sep:/
                            set:x:/@join?value
                              src:/{0}/
                                :x:/@join?value
                            trim-left:x:/@join?value
                              chars:/
                            set:x:/@.folder?value
                              src:/{0}
                                :x:/@trim-left?value

                            /*
                             * Serializing form to figure out new name for file.
                             */
                            micro.form.serialize:hyper-ide-rename-file-form

                            /*
                             * Renaming file.
                             */
                            move-file:x:/@.filename?value
                              dest:{0}{1}
                                :x:/@.folder?value
                                :x:/@micro.form.serialize/*/name?value

                            /*
                             * Refreshing tree, since folder's content now has changed.
                             */
                            add:x:/../*/micro.widgets.tree.refresh-items/*/items
                              src:x:/@.folder?value
                            micro.widgets.tree.refresh-items:hyper-ide-folder-tree-browser
                              force-expand:bool:true
                              items

                            /*
                             * Changes name of active file.
                             */
                            hyper-ide.set-active-filename:{0}{1}
                              :x:/@.folder?value
                              :x:/@micro.form.serialize/*/name?value

                            /*
                             * Selecting (updated) file in tree view now.
                             */
                            add:x:/../*/micro.widgets.tree.select-items/*/items
                              src:{0}{1}
                                :x:/@.folder?value
                                :x:/@micro.form.serialize/*/name?value
                            micro.widgets.tree.select-items:hyper-ide-folder-tree-browser
                              items

                            /*
                             * Deleting modal widget, and giving user some feedback.
                             */
                            delete-widget:hyper-ide-confirm-rename-file

                        button
                          innerValue:Cancel
                          onclick

                            /*
                             * Simply deleting modal widget.
                             */
                            delete-widget:hyper-ide-confirm-rename-file

      /*
       * Deletes file.
       */
      button
        innerValue:@"<span class=""icon-bin""></span>"
        title:Deletes your file
        onclick

          /*
           * Deleting file.
           * First retrieving file name.
           */
          p5.web.widgets.get-parent:x:/../*/_event?value
          get-widget-property:x:/-/*/*?value
            .toolbar

          /*
           * Then passing filename into modal widget.
           */
          eval-x:x:/+/**/.filename
          create-widgets
            micro.widgets.modal:hyper-ide-confirm-delete-file
              widgets
                h3
                  innerValue:Confirm deletion
                literal
                  element:p
                  style:"float:left;font-size:5rem;margin-right:.5rem;"
                  class:icon-bin
                p
                  innerValue:@"Are you sure you want to delete the <code>{0}</code> file? I need you to confirm this action before I allow you to do this."
                    :x:/@get-widget-property/*/*?value
                p
                  innerValue:@"<strong>Warning, this action is permanent!</strong>"
                div
                  class:right
                  widgets
                    div
                      class:strip
                      widgets
                        button
                          innerValue:Yes
                          oninit

                            /*
                             * Setting initial focus to "Yes" button.
                             */
                            micro.page.set-focus:x:/../*/_event?value

                          onclick

                            /*
                             * Forward evaluated above.
                             */
                            .filename:x:/../*/get-widget-property/*/*?value

                            /*
                             * Deleting actual file.
                             */
                            delete-file:x:/@.filename?value

                            /*
                             * Deleting selected item from tree.
                             *
                             * Notice, selected tree view item will always be the file or folder 
                             * user is trying to delete, since selecting a tree view item, 
                             * changes the active file object edited.
                             */
                            micro.widgets.tree.get-selected-items:hyper-ide-folder-tree-browser
                            add:x:/../*/micro.widgets.tree.delete-items
                              src:x:/@micro.widgets.tree.get-selected-items/*
                            micro.widgets.tree.delete-items:hyper-ide-folder-tree-browser

                            /*
                             * Making sure we close all editor for file.
                             */
                            eval-x:x:/+/*
                            hyper-ide.editors.close-all
                              filter:x:/@.filename?value
                              exact:bool:true

                            /*
                             * Deleting modal window.
                             */
                            delete-widget:hyper-ide-confirm-delete-file

                        button
                          innerValue:No
                          onclick

                            /*
                             * Simply deleting modal widget.
                             */
                            delete-widget:hyper-ide-confirm-delete-file

      /*
       * Downloads file.
       */
      button
        innerValue:@"<span class=""icon-download2""></span>"
        title:Download file
        onclick

          /*
           * Downloading file.
           */
          p5.web.widgets.get-parent:x:/../*/_event?value
          get-widget-property:x:/-/*/*?value
            .toolbar
          micro.download.file:x:/-/*/*?value

  /*
   * Activate tab/editor button.
   */
  button
    parent:hyper-ide-editor-tab-buttons
    class:hyper-ide-activate-tab-button toggled
    .activate:x:/../*/file?value
    data-activate:x:/../*/file?value
    title:x:/../*/file?value
    oninit

      /*
       * Making sure we wire up onclick eventhandler for widget.
       */
      set-widget-property:x:/../*/_event?value
        onclick:@"return p5.hyper_ide_show_editor ('{0}');"
          :x:/../*/_event?value

    events


      /*
       * Invoked when for some reasons active file changes name.
       */
      hyper-ide.set-active-filename

        /*
         * Checking if this is our active file, and if not, returning early.
         */
        get-widget-property:x:/../*/_event?value
          class
        if:x:/@get-widget-property/*/*?value
          !~:toggled
          return

        /*
         * Updating [.file] attribute for widget.
         */
        split:x:/../*/_arg?value
          =:/
        p5.web.widgets.get-children:x:/../*/_event?value
        set-widget-property:x:/-/*/0?value
          innerValue:x:/@split/0/-?name
        set-widget-property:x:/../*/_event?value
          .activate:x:/../*/_arg?value
          data-value:x:/../*/_arg?value
          title:x:/../*/_arg?value

    .onclick

      /*
       * Getting filename for editor.
       */
      get-widget-property:x:/../*/_event?value
        .activate

      /*
       * Activating editor, and verifying invocation was a success.
       */
      hyper-ide.editors.set-active-editor:x:/@get-widget-property/*/*?value
      if:x:/-?value

        /*
         * Making sure we scroll tree view item into view.
         */
        p5.web.widgets.find-first-like:hyper-ide-folder-tree-browser
          class:tree-selected

    widgets
      span
        innerValue:x:/@split/0/-?name

      /*
       * Close editor button.
       */
      literal
        element:span
        class:hyper-ide-close icon-cross button
        title:Close editor
        role:button
        onclick

          /*
           * Closing active editor.
           */
          p5.web.widgets.get-parent:x:/../*/_event?value
          get-widget-property:x:/-/*/*?value
            .activate
          eval-x:x:/+/*
          hyper-ide.editors.close-all
            filter:x:/@get-widget-property/*/*?value

  /*
   * Actual editor for file.
   *
   * Dynamically created above according to file extension.
   */
  div
    parent:hyper-ide-editor-tab-editors
    class:hyper-ide-codemirror-wrapper visible
    .editor:x:/../*/file?value
    data-editor:x:/../*/file?value
    events

      /*
       * Invoked when for some reasons active file changes name.
       */
      hyper-ide.set-active-filename

        /*
         * Checking if this is our active editor, and if not, returning early.
         */
        get-widget-property:x:/../*/_event?value
          class
        if:x:/@get-widget-property/*/*?value
          ~:hide
          return

        /*
         * Updating [.file] attribute for widget.
         */
        set-widget-property:x:/../*/_event?value
          .editor:x:/../*/_arg?value

    widgets:editor
