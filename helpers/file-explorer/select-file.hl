/*
 * This file is invoked when the user clicks a file in the file explorer.
 * The file expects a __[file]__ argument being the fully qualified path
 * of the file user clicked, or selected somehow.
 *
 * This file will edit the specified __[file]__, using its associated
 * CodeMirror editor - If a mapping between the file's file extension and a
 * CodeMirror mode exists.
 */





/*
 * Signal node to separate arguments from the rest of our lambda.
 */
.signal





/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  file:string





/*
 * Deleting any previously created edit active folder object toolbars.
 */
if
  fetch:x:/0/0?value
    widget-exists:hyper-ide-active-folder-toolbar

  /*
   * A folder is currently edited, deleting active file object toolbar for
   * the folder that is edited.
   */
  delete-widget:hyper-ide-active-folder-toolbar





/*
 * Checking if file is already open, at which point we simply activate existing
 * editor.
 */
p5.web.widgets.find:hyper-ide-editor-tab-buttons
  .activate:x:/../*/file?value
if:x:/@p5.web.widgets.find/*/*?value

  /*
   * Editor for file is already open, activating editor, and returning early.
   */
  eval-x:x:/+/*
  micro.evaluate.file:@IDE/helpers/activate-editor.hl
    file:x:/../*/file?value

else

  /*
   * Editing file.
   */
  add:x:/+
    src:x:/@.signal/--/<-
  micro.evaluate.file:@IDE/helpers/edit-file.hl
