
/*
 * File responsible for starting app.
 *
 * This file is evaluated when the user clicks the desktop icon.
 */





/*
 * Including Micro, its "serious" skin, font CSS file, and main CSS file for Hypereval.
 */
p5.web.include-css-file
  @MICRO/media/main.css
  @MICRO/media/skins/serious.css
  @MICRO/media/fonts.css





/*
 * Verifying user is logged in as root, and if not, 
 * forcing the user to login before we proceed.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not logged in as root.
   */
  p5.core.login
    message:You'll need to login with a root account to access this module

  /*
   * Returning early to abort evaluating the rest of our file.
   */
  return





/*
 * Creating main container, making sure we set the title of our page.
 */
p5.web.page.set-title:Hyper IDE
create-widget:hyper-ide-main-container
  class:container-fullscreen
  widgets

    /*
     * Wrapper for our top toolbar.
     */
    div
      class:row
      widgets
        div
          class:col-100
          widgets
            h3
              innerValue:Hyper IDE
              style:"float:left;"
            div
              class:right
              widgets
                div
                  class:strip toolbar
                  style:"display:inline-block;"
                  widgets
                    button
                      innerValue:@"<span class=""icon-eye""></span>"
                      title:Toggles visibility of file browser
                      class:toggled
                      onclick

                        /*
                         * Checking if we should hide or display file browser.
                         */
                        get-widget-property:hyper-ide-file-browser-wrapper
                          visible
                        micro.css.toggle:hyper-ide-file-browser-wrapper
                          class:hide
                        micro.css.toggle:x:/../*/_event?value
                          class:toggled

                    button
                      innerValue:@"<span class=""icon-home3""></span>"
                      title:Close Hypereval
                      onclick

                        /*
                         * Redirecting user to server's root URL.
                         */
                        p5.web.get-root-location
                        p5.web.set-location:x:/-?value

    /*
     * Wrapper for actual content.
     */
    div
      class:row
      widgets

        /*
         * Wrapper for our file browser.
         */
        div:hyper-ide-file-browser-wrapper
          class:col-40
          widgets
            container
              class:bg air-inner shaded rounded
              widgets

                /*
                 * Our TreeView that actually allows the user to browse 
                 * files on his server.
                 */
                micro.widgets.tree:hyper-ide-folder-tree-browser
                  items
                    /:/

                  /*
                   * Callback invoked when TreeView wants more items.
                   */
                  .onexpand

                    /*
                     * Checking if this is a request for a folder.
                     */
                    if
                      ends-with:x:/../*/_item-id?value
                        src:/

                      /*
                       * Invoking file responsible for actually retrieving files
                       * and folders structure, and returning items to caller.
                       */
                      eval-x:x:/+/*
                      micro.evaluate.file:@IDE/helpers/get-files.hl
                        folder:x:/../*/_item-id?value
                      add:x:/..if/*/return/*/items
                        src:x:/@micro.evaluate.file/*
                      return
                        items

                  /*
                   * Callback invoked when an item ise selected.
                   */
                  .onselect

                    /*
                     * Checking if this is a request for a folder or a file.
                     */
                    if
                      ends-with:x:/../*/items/0?name
                        src:/

                      /*
                       * Request for folder.
                       *
                       * Invoking file responsible for selecting the folder.
                       */
                      eval-x:x:/+/*
                      micro.evaluate.file:@IDE/helpers/select-folder.hl
                        folder:x:/../*/items/0?name

                    else

                      /*
                       * Request for file.
                       *
                       * Invoking file responsible for selecting the folder.
                       */
                      eval-x:x:/+/*
                      micro.evaluate.file:@IDE/helpers/select-file.hl
                        file:x:/../*/items/0?name

        /*
         * Wrapper for our file editor.
         */
        div
          class:col
          widgets
            container:hyper-ide-editor
              class:bg air-inner shaded rounded
              style:"overflow:auto;"
              widgets
                h3
                  innerValue:Active object
                container:hyper-ide-active-object-wrapper
