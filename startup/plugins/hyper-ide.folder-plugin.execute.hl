/*
 * Create our execute in folder plugin button.
 */
create-event:hyper-ide.folder-plugins.execute
  return
    button
      innerValue:@"<span class=""icon-terminal""></span>"
      title:Open terminal window
      onclick

        /*
         * Retrieving current folder.
         */
        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
          .folder
        get-widget-property:x:/-/*/*?value
          .folder

        /*
         * Retrieving active code.
         */
        p5.web.session.get:hyper-ide.terminal-code
        if:x:/@p5.web.session.get/*?value
          set:x:/../*/.code?value
            src:x:/@p5.web.session.get/*?value
        .code:

        /*
         * Opens a modal window, asking user for command(s) he wants to execute.
         */
        eval-x:x:/+/**/.folder|/+/**/.code
        create-widgets
          micro.widgets.modal:hyper-ide-execute-modal
            widgets
              micro.widgets.obscurer:hyper-ide-eval-shell-obscurer
                style:"display:none;"
                message:Executing your script ...
              h3
                innerValue:Shell script
                oninit

                  /*
                   * Forward evaluated above.
                   */
                  .folder:x:/../*/get-widget-property/*/*?value

                  /*
                   * Displaying working folder as part of header.
                   */
                  set-widget-property:x:/../*/_event?value
                    innerValue:x:/@.folder?value

              div
                class:strip fill air-top
                widgets

                  /*
                   * Template snippets selector.
                   */
                  label
                    innerValue:Template
                  select
                    oninit

                      /*
                       * Listing all files in "shell-snippets" folder, and creating
                       * one option element for each.
                       */
                      list-files:@IDE/helpers/shell-snippets/
                        filter:.sh
                      for-each:x:/@list-files/*?name
                        split:x:/@_dp?value
                          =:/
                          =:.
                        create-widget
                          element:option
                          parent:x:/../*/_event?value
                          innerValue:x:/@split/0/-2?name
                          value:x:/@_dp?value

                    onchange

                      /*
                       * Loading file, and setting value of CodeMirror editor.
                       */
                      get-widget-property:x:/../*/_event?value
                        value
                      load-file:x:/@get-widget-property/*/*?value
                      replace:x:/@load-file/*?value
                        src:`
                        dest:\`
                      replace:x:/@replace?value
                        src:$
                        dest:\$
                      p5.web.widgets.find:hyper-ide-terminal-commands
                        element:textarea
                      p5.web.send-javascript:@"p5['{0}'].getDoc().setValue(`{1}`);"
                        :x:/@p5.web.widgets.find/*/*?value
                        :x:/@replace?value

                    widgets
                      option
                        innerValue:Select template snippet ...

              micro.widgets.codemirror:hyper-ide-terminal-commands
                .data-field:code
                auto-focus:true
                mode:shell
                value:x:/../*/.code?value

              div
                class:right air-top
                widgets
                  div
                    class:strip
                    widgets

                      /*
                       * Execute button.
                       */
                      button:hyper-ide-execute-shell-script-button
                        innerValue:Execute
                        onclick

                          /*
                           * Making sure we show obscurer, for then to invoke "real" implementation.
                           */
                          set-widget-property:hyper-ide-eval-shell-obscurer
                            style:"display:block;position:absolute;"
                          p5.web.send-javascript:@"p5.$('hyper-ide-execute-shell-script-button').raise('.onclick');"

                        .onclick

                          /*
                           * Forward evaluated above.
                           */
                          .folder:x:/../*/get-widget-property/*/*?value

                          /*
                           * Retrieving script.
                           */
                          micro.form.serialize:hyper-ide-execute-modal

                          /*
                           * Saving current code in session, since it's highly likely that
                           * user wants to execute similar code the next time he or she opens this window.
                           */
                          p5.web.session.set:hyper-ide.terminal-code
                            src:x:/@micro.form.serialize/*/code?value

                          /*
                           * Checking platform, since we might need to convert
                           * from CR/LF to only LF, in case we're on MacOSX or Unix.
                           */
                          p5.system.platform
                          if:x:/@p5.system.platform?value
                            =:Unix
                            or:x:/@p5.system.platform?value
                              =:MacOSX

                            /*
                             * Replacing CR/LF before we save file to get platform specific CR sequence.
                             */
                            p5.system.platform.normalize-string:x:/@micro.form.serialize/*/code?value
                            save-file:~/temp/temp-bash.sh
                              src:x:/@p5.system.platform.normalize-string?value

                          else

                            /*
                             * Saving file with CR/LF as new line characters.
                             */
                            save-file:~/temp/temp-bash.sh
                              src:x:/@micro.form.serialize/*/code?value

                          /*
                           * In case temp file contains passwords and such, we want
                           * to make 100% certain of that we can successfully delete
                           * it after execution, even if an exception occurs.
                           */
                          try

                            /*
                             * Executing file with current folder being the currently open folder.
                             */
                            p5.system.platform.execute-file:~/temp/temp-bash.sh
                              working-folder:x:/@.folder?value

                            /*
                             * Displaying results of evaluation, making sure we
                             * HTML encode it, to be sure.
                             */
                            p5.html.html-encode:x:/@p5.system.platform.execute-file/*?value
                            set-widget-property:hyper-ide-execute-result
                              visible:true
                              class:success air-top
                              innerValue:x:/@p5.html.html-encode?value

                          catch

                            /*
                             * Displaying error to user.
                             */
                            p5.html.html-encode:x:/@message?value
                            set-widget-property:hyper-ide-execute-result
                              visible:true
                              class:warning air-top
                              innerValue:x:/@p5.html.html-encode?value

                          finally

                            /*
                             * Deleting temporary file.
                             */
                            delete-file:~/temp/temp-bash.sh

                            /*
                             * Making sure we hide obscurer.
                             */
                            set-widget-property:hyper-ide-eval-shell-obscurer
                              style:"display:none;"

                      /*
                       * Close window button.
                       */
                      button
                        innerValue:Close
                        onclick

                          /*
                           * Simply closing modal window, making sure we save the active code
                           * in our session at the same time.
                           */
                          delete-widget:hyper-ide-execute-modal

              literal:hyper-ide-execute-result
                class:air-top
                style:"font-size:.7rem;"
                element:pre
                visible:false
