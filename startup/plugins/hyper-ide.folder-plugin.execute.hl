/*
 * Create our execute in folder plugin button.
 */
create-event:hyper-ide.folder-plugins.execute
  return
    button
      innerValue:@"<span class=""icon-flash""></span>"
      style:"margin-bottom:0;"
      title:Open terminal window
      onclick

        /*
         * Retrieving current folder.
         */
        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
          .folder
        get-widget-property:x:/-/*/*?value
          .folder

        /*
         * Opens a modal window, asking user for command(s) he wants to execute.
         */
        eval-x:x:/+/**/.folder
        create-widgets
          micro.widgets.modal:hyper-ide-execute-modal
            widgets
              h3
                innerValue:Shell script
                oninit

                  /*
                   * Forward evaluated above.
                   */
                  .folder:x:/../*/get-widget-property/*/*?value

                  /*
                   * Displaying working folder as part of header.
                   */
                  set-widget-property:x:/../*/_event?value
                    innerValue:"$ {0}"
                      :x:/@.folder?value

              div
                class:right air-top
                widgets
                  div
                    class:strip fill
                    widgets

                      /*
                       * Template snippets selector.
                       */
                      label
                        innerValue:Template
                      select
                        oninit

                          /*
                           * Listing all files in "shell-snippets" folder, and creating
                           * one option element for each.
                           */
                          list-files:@IDE/helpers/shell-snippets/
                            filter:.sh
                          for-each:x:/@list-files/*?name
                            split:x:/@_dp?value
                              =:/
                              =:.
                            create-widget
                              element:option
                              parent:x:/../*/_event?value
                              innerValue:x:/@split/0/-2?name
                              value:x:/@_dp?value

                        onchange

                          /*
                           * Loading file, and setting value of CodeMirror editor.
                           */
                          get-widget-property:x:/../*/_event?value
                            value
                          load-file:x:/@get-widget-property/*/*?value
                          replace:x:/@load-file/*?value
                            src:`
                            dest:\`
                          replace:x:/@replace?value
                            src:$
                            dest:\$
                          p5.web.widgets.find:hyper-ide-terminal-commands
                            element:textarea
                          p5.web.send-javascript:@"p5['{0}'].getDoc().setValue(`{1}`);"
                            :x:/@p5.web.widgets.find/*/*?value
                            :x:/@replace?value

                        widgets
                          option
                            innerValue:Select template snippet ...

              micro.widgets.codemirror:hyper-ide-terminal-commands
                .data-field:code
                auto-focus:true
                mode:shell

              div
                class:right air-top
                widgets
                  div
                    class:strip
                    style:"display:inline-block;"
                    widgets

                      /*
                       * Execute button.
                       */
                      button
                        innerValue:Execute
                        style:"margin-bottom:0;"
                        onclick

                          /*
                           * Forward evaluated above.
                           */
                          .folder:x:/../*/get-widget-property/*/*?value

                          /*
                           * Saving editor's content to a temporary file.
                           */
                          micro.form.serialize:hyper-ide-execute-modal
                          save-file:~/temp/temp-bash.sh
                            src:x:/@micro.form.serialize/*/code?value

                          /*
                           * In case temp file contains passwords and such, we want
                           * to make 100% certain of that we can successfully delete
                           * it after execution, even if an exception occurs.
                           */
                          try

                            /*
                             * Executing file with current folder being the currently open folder.
                             */
                            execute-file:~/temp/temp-bash.sh
                              working-folder:x:/@.folder?value

                            /*
                             * Displaying results of evaluation, making sure we
                             * HTML encode it, to be sure.
                             */
                            p5.html.html-encode:x:/@execute-file/*?value
                            set-widget-property:hyper-ide-execute-result
                              visible:true
                              class:success air-top
                              innerValue:x:/@p5.html.html-encode?value

                          catch

                            /*
                             * Displaying error to user.
                             */
                            p5.html.html-encode:x:/@message?value
                            set-widget-property:hyper-ide-execute-result
                              visible:true
                              class:warning air-top
                              innerValue:x:/@p5.html.html-encode?value

                          finally

                            /*
                             * Deleting temporary file.
                             */
                            delete-file:~/temp/temp-bash.sh

                      /*
                       * Close window button.
                       */
                      button
                        innerValue:Close
                        style:"margin-bottom:0;"
                        onclick

                          /*
                           * Simply closing modal window.
                           */
                          delete-widget:hyper-ide-execute-modal

              literal:hyper-ide-execute-result
                class:air-top
                style:"margin-bottom:0;font-size:.7rem;"
                element:pre
                visible:false
