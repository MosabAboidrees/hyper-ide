
/*
 * Create our minify JavaScript plugin for editor.
 */
create-event:hyper-ide.plugins.js.minify
  return
    button
      innerValue:@"<span class=""icon-file-zip""></span>"
      style:"margin-bottom:0;"
      title:Minifies your JavaScript
      onclick
        /*
         * Retrieves code for currently visible editor, minifies it, and updates 
         * value of editor.
         *
         * Notice, we also calculate length of both original version and minified version,
         * to give user some feedback about how many percent of the code was eliminated.
         */
        hyper-ide.get-code
        length:x:/@hyper-ide.get-code?value
        p5.web.javascript.minify:x:/@hyper-ide.get-code?value
        length:x:/@p5.web.javascript.minify?value

        /*
         * Unless we could remove something during minification, we inform user,
         * and return early.
         */
        if:x:/../*/length/[0,1]?value.decimal
          =:x:/../*/length/[1,2]?value.decimal

          /*
           * No further minification is possible.
           */
          micro.windows.info:No further minification is possible
            class:micro-windows-info warning
          return

        /*
         * Updating code.
         */
        hyper-ide.set-code:x:/@p5.web.javascript.minify?value

        /*
         * Notifying user, making sure we inform him of how many percent of the code was removed.
         */
        /:x:/../*/length/[1,2]?value.decimal
          _:x:/../*/length/[0,1]?value.decimal
        -:decimal:1
          _:x:/./-?value
        *:x:/-?value
          _:100
        micro.windows.info:"JavaScript was successfully minified, we saved {0}%"
          :x:/@*?value.int
          class:micro-windows-info success
